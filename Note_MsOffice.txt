/******************************************************************************/
Excel表格公式大全∼∼這太有用了∼大推
/******************************************************************************/
2014-05-10 / 作者 : 啵啵 / 690760
1、查找重複內容公式：=IF(COUNTIF(A:A,A2)>1,"重複","")。
2、用出生年月來計算年齡公式：=TRUNC((DAYS360(H6,"2009/8/30",FALSE))/360,0)。
3、從輸入的18位身份證號的出生年月計算公式：=CONCATENATE(MID(E2,7,4),"/",MID(E2,11,2),"/",MID(E2, 13,2))。
4、從輸入的身份證號碼內讓系統自動提取性別，可以輸入以下公式：
   =IF(LEN(C2)=15,IF(MOD(MID(C2,15,1),2)=1,"男","女"),IF(MOD(MID(C2,17,1), 2)=1,"男","女"))公式內的「C2」代表的是輸入身份證號碼的單元格。

1、求和： =SUM(K2:K56) ——對K2到K56這一區域進行求和；
2、平均數： =AVERAGE(K2:K56) ——對K2 K56這一區域求平均數；
3、排名： =RANK(K2，K$2:K$56) ——對55名學生的成績進行排名；
4、等級： =IF(K2>=85,"優",IF(K2>=74,"良",IF(K2>=60,"及格","不及格")))
5、學期總評： =K2*0.3+M2*0.3+N2*0.4 ——假設K列、M列和N列分別存放著學生的「平時總評」、「期中」、「期末」三項成績；
6、最高分： =MAX(K2:K56) ——求K2到K56區域（55名學生）的最高分；
7、最低分： =MIN(K2:K56) ——求K2到K56區域（55名學生）的最低分；
8、分數段人數統計：
（1） =COUNTIF(K2:K56,"100") ——求K2到K56區域100分的人數；假設把結果存放於K57單元格；
（2） =COUNTIF(K2:K56,">=95")－K57 ——求K2到K56區域95∼99.5分的人數；假設把結果存放於K58單元格；
（3）=COUNTIF(K2:K56,">=90")－SUM(K57:K58) ——求K2到K56區域90∼94.5分的人數；假設把結果存放於K59單元格；
（4）=COUNTIF(K2:K56,">=85")－SUM(K57:K59) ——求K2到K56區域85∼89.5分的人數；假設把結果存放於K60單元格；
（5）=COUNTIF(K2:K56,">=70")－SUM(K57:K60) ——求K2到K56區域70∼84.5分的人數；假設把結果存放於K61單元格；
（6）=COUNTIF(K2:K56,">=60")－SUM(K57:K61) ——求K2到K56區域60∼69.5分的人數；假設把結果存放於K62單元格；
（7） =COUNTIF(K2:K56,"<60") ——求K2到K56區域60分以下的人數；假設把結果存放於K63單元格；
說明：COUNTIF函數也可計算某一區域男、女生人數。
如：=COUNTIF(C2:C351,"男") ——求C2到C351區域（共350人）男性人數；
9、優秀率： =SUM(K57:K60)/55*100
10、及格率： =SUM(K57:K62)/55*100
11、標準差： =STDEV(K2:K56) ——求K2到K56區域(55人)的成績波動情況（數值越小，說明該班學生間的成績差異較小，反之，說明該班存在兩極分化）；
12、條件求和： =SUMIF(B2:B56,"男"，K2:K56) ——假設B列存放學生的性別，K列存放學生的分數，則此函數返回的結果表示求該班男生的成績之和；
13、多條件求和： ｛=SUM(IF(C3:C322="男",IF(G3:G322=1,1,0)))｝ ——假設C列（C3:C322區域）存放學生的性別，G列（G3:G322區域）存放學生所在班級代碼（1、2、3、4、5），則此函數返回的結果表示求一班的男生人數；這是一個數組函數，輸完後要按Ctrl＋Shift＋Enter組合鍵(產生「｛……｝」)。「｛｝」不能手工輸入，只能用組合鍵產生。
14、根據出生日期自動計算週歲：=TRUNC((DAYS360(D3,NOW( )))/360,0)
———假設D列存放學生的出生日期，E列輸入該函數後則產生該生的週歲。
15、在Word中三個小竅門：
１連續輸入三個「~」可得一條波浪線。
２連續輸入三個「-」可得一條直線。
連續輸入三個「=」可得一條雙直線。

一、excel中當某一單元格符合特定條件，如何在另一單元格顯示特定的顏色比如：
A1〉1時，C1顯示紅色
0<A1<1時，C1顯示綠色
A1<0時，C1顯示黃色
方法如下：
1、單元擊C1單元格，點「格式」>「條件格式」，條件1設為：
公式=A1=1 
2、點「格式」->「字體」->「顏色」，點擊紅色後點「確定」。
條件2設為：
公式=AND(A1>0,A1<1) 
3、點「格式」->「字體」->「顏色」，點擊綠色後點「確定」。
條件3設為：
公式=A1點「格式」->「字體」->「顏色」，點擊黃色後點「確定」。
4、三個條件設定好後，點「確定」即出。

二、EXCEL中如何控制每列數據的長度並避免重複錄入
1、用數據有效性定義數據長度。
用滑鼠選定你要輸入的數據範圍，點"數據"->"有效性"->"設置"，"有效性條件"設成"允許""文本長度""等於""5"（具體條件可根據你的需要改變）。
還可以定義一些提示資訊、出錯警告資訊和是否打開中文輸入法等，定義好後點"確定"。
2、用條件格式避免重複。
選定A列，點"格式"->"條件格式"，將條件設成「公式=COUNTIF($A:$A,$A1)>1」，點"格式"->"字體"->"顏色"，選定紅色後點兩次"確定"。
這樣設定好後你輸入數據如果長度不對會有提示，如果數據重複字體將會變成紅色。

三、在EXCEL中如何把B列與A列不同之處標識出來？
（一）、如果是要求A、B兩列的同一行數據相比較：
假定第一行為表頭，單擊A2單元格，點「格式」->「條件格式」，將條件設為: 
「單元格數值」 「不等於」=B2 
點「格式」->「字體」->「顏色」，選中紅色，點兩次「確定」。
用格式刷將A2單元格的條件格式向下複製。
B列可參照此方法設置。
（二）、如果是A列與B列整體比較（即相同數據不在同一行）：
假定第一行為表頭，單擊A2單元格，點「格式」->「條件格式」，將條件設為: 
「公式」=COUNTIF($B:$B,$A2)=0 
點「格式」->「字體」->「顏色」，選中紅色，點兩次「確定」。
用格式刷將A2單元格的條件格式向下複製。
B列可參照此方法設置。
按以上方法設置後，AB列均有的數據不著色，A列有B列無或者B列有A列無的數據標記為紅色字體。

四、EXCEL中怎樣批量地處理按行排序
假定有大量的數據(數值)，需要將每一行按從大到小排序，如何操作？
由於按行排序與按列排序都是只能有一個主關鍵字,主關鍵字相同時才能按次關鍵字排序。所以，這一問題不能用排序來解決。解決方法如下：
1、假定你的數據在A至E列，請在F1單元格輸入公式：
=LARGE($A1:$E1,COLUMN(A1)) 
用填充柄將公式向右向下複製到相應範圍。
你原有數據將按行從大到小排序出現在F至J列。如有需要可用「選擇性粘貼/數值」複製到其他地方。
註：第1步的公式可根據你的實際情況（數據範圍）作相應的修改。如果要從小到大排序,公式改為:=SMALL($A1:$E1,COLUMN(A1))

五、巧用函數組合進行多條件的計數統計
例：第一行為表頭，A列是「姓名」，B列是「班級」，C列是「語文成績」，D列是「錄取結果」，現在要統計「班級」為「二」，「語文成績」大於等於104，「錄取結果」為「重本」的人數。統計結果存放在本工作表的其他列。
公式如下：
=SUM(IF((B2:B9999="二")*(C2:C9999>=104)*(D2:D9999="重本"),1,0)) 
輸入完公式後按Ctrl+ Shift+Enter鍵,讓它自動加上數組公式符號"{}"。

六、如何判斷單元格裡 ??是否包含指定文本？
假定對A1單元格進行判斷有無"指定文本",以下任一公式均可: 
=IF(COUNTIF(A1,"*"&"指定文本"&"*")=1,"有","無") 
=IF(ISERROR(FIND("指定文本",A1,1)),"無","有")
求某一區域內不重複的數據個數
例如求A1:A100範圍內不重複數據的個數，某個數重複多次出現只算一個。有兩種計算方法：
一是利用數組公式：
=SUM(1/COUNTIF(A1:A100,A1:A100)) 
輸入完公式後按Ctrl+Shift+Enter鍵,讓它自動加上數組公式符號"{ }"。
二是利用乘積求和函數：
=SUMPRODUCT(1/COUNTIF(A1:A100,A1:A100))

七、一個工作薄中有許多工作表如何快速整理出一個目錄工作表
1、用宏3.0取出各工作表的名稱，方法：
Ctrl+F3出現自定義名稱對話方塊，取名為X，在「引用位置」框中輸入：
=MID(GET.WORKBOOK(1),FIND( "]",GET.WORKBOOK(1))+1,100) 
確定
2、用HYPERLINK函數批量插入連接，方法：
在目錄工作表（一般為第一個sheet）的A2單元格輸入公式：
=HYPERLINK("# '"&INDEX(X,ROW())&"'!A1",INDEX(X,ROW())) 
將公式向下填充，直到出錯為止，目錄就生成了。




/******************************************************************************/
/******************************************************************************/
/******************************************************************************/
/******************************************************************************/
/******************************************************************************/
/******************************************************************************/
/******************************************************************************/
/******************************************************************************/
/******************************************************************************/
/******************************************************************************/
/******************************************************************************/
